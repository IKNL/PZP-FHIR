map "http://hl7.org/fhir/StructureMap/ImagingStudy4to3" = "R4 to R3 Conversion for ImagingStudy"

uses "http://hl7.org/fhir/StructureDefinition/ImagingStudy" alias ImagingStudy as source
uses "http://hl7.org/fhir/3.0/StructureDefinition/ImagingStudy" alias ImagingStudyR3 as target

imports "http://hl7.org/fhir/StructureMap/*4to3"

group ImagingStudy(source src : ImagingStudyR3, target tgt : ImagingStudy) extends DomainResource <<type+>> {
  src.identifier as vs where system != 'urn:dicom:uid' -> tgt.identifier as vt then Identifier(vs, vt);
  src.identifier as vs where system = 'urn:dicom:uid' then {
    vs.value -> tgt.uid;
  };
  src.status where value = 'registered' -> tgt.availability = 'OFFLINE';
  src.status where value = 'available' -> tgt.availability = 'ONLINE';
  src.modality -> tgt.modalityList;
  src.subject -> tgt.patient;
  src.encounter -> tgt.context;
  src.started as vs -> tgt.started as vt then string(vs, vt);
  src.basedOn as vs -> tgt.basedOn as vt then string(vs, vt);
  src.referrer as vs -> tgt.referrer as vt then string(vs, vt);
  src.interpreter as vs -> tgt.interpreter as vt then string(vs, vt);
  src.endpoint as vs -> tgt.endpoint as vt then string(vs, vt);
  src.numberOfSeries as vs -> tgt.numberOfSeries as vt then string(vs, vt);
  src.numberOfInstances as vs -> tgt.numberOfInstances as vt then string(vs, vt);
  src.procedureReference as vs -> tgt.procedureReference as vt then string(vs, vt);
  src.procedureCode as vs -> tgt.procedureCode as vt then string(vs, vt);
  src.reasonCode -> tgt.reason;
  src.description as vs -> tgt.description as vt then markdown(vs, vt);
  src.series as vs -> tgt.series as vt then series(vs, vt);
}

group series(source src, target tgt) extends BackboneElement {
  src.uid as vs -> tgt.uid as vt then string(vs, vt);
  src.number as vs -> tgt.number as vt then string(vs, vt);
  src.modality as vs -> tgt.modality as vt then string(vs, vt);
  src.description as vs -> tgt.description as vt then markdown(vs, vt);
  src.numberOfInstances as vs -> tgt.numberOfInstances as vt then string(vs, vt);
  src.extension as e where url = 'http://hl7.org/fhir/3.0/StructureDefinition/extension-ImagingStudy.series.availability' then {
    e.value -> tgt.availability;
  };
  src.endpoint as vs -> tgt.endpoint as vt then string(vs, vt);
  src.bodySite as vs -> tgt.bodySite as vt then CodeableConcept(vs, vt);
  src.laterality as vs -> tgt.laterality as vt then string(vs, vt);
  src.started as vs -> tgt.started as vt then string(vs, vt);
  src.performer as vs where function.coding.where((system = 'http://terminology.hl7.org/CodeSystem/v3-ParticipationType') and (code = 'PRF')) then {
    vs.actor -> tgt.performer;
  };
  src.instance as vs -> tgt.instance as vt then instance(vs, vt);
}

group instance(source src, target tgt) extends BackboneElement {
  src.uid as vs -> tgt.uid as vt then string(vs, vt);
  src.number as vs -> tgt.number as vt then string(vs, vt);
  src.sopClass as s where system = 'urn:ietf:rfc:3986' then {
    s.code -> tgt.sopClass;
  };
  src.title as vs -> tgt.title as vt then string(vs, vt);
}

