map "http://hl7.org/fhir/StructureMap/StructureDefinition4to3" = "R4 to R3 Conversion for StructureDefinition"

uses "http://hl7.org/fhir/StructureDefinition/StructureDefinition" alias StructureDefinition as source
uses "http://hl7.org/fhir/StructureDefinition/StructureDefinition" alias StructureDefinitionR3 as target

imports "http://hl7.org/fhir/StructureMap/*4to3"

group StructureDefinition(source src : StructureDefinition, target tgt : StructureDefinitionR3) extends DomainResource <<type+>> {
  src.url as vs -> tgt.url = vs;
  src.identifier as vs -> tgt.identifier as vt then Identifier(vs, vt);
  src.version as vs -> tgt.version as vt then string(vs, vt);
  src.name as vs -> tgt.name = vs;
  src.title as vs -> tgt.title as vt then string(vs, vt);
  src.status as vs -> tgt.status as vt then code(vs, vt);
  src.experimental as vs -> tgt.experimental as vt then boolean(vs, vt);
  src.date as vs -> tgt.date as vt then dateTime(vs, vt);
  src.publisher as vs -> tgt.publisher as vt then string(vs, vt);
  src.contact as vs -> tgt.contact as vt then ContactDetail(vs, vt);
  src.description as vs -> tgt.description as vt then markdown(vs, vt);
  src.useContext as vs -> tgt.useContext as vt then string(vs, vt);
  src.jurisdiction as vs -> tgt.jurisdiction as vt then string(vs, vt);
  src.purpose as vs -> tgt.purpose as vt then markdown(vs, vt);
  src.copyright as vs -> tgt.copyright as vt then markdown(vs, vt);
  src.keyword as vs -> tgt.keyword as vt then string(vs, vt);
  src.fhirVersion as vs -> tgt.fhirVersion = '3.0.2';
  src.mapping as vs0 -> tgt.mapping as vt0 then {
    vs0.identity as vs1 -> vt0.identity as vt1 then id(vs1, vt1);
    vs0.uri as vs1 -> vt0.uri as vt1 then uri(vs1, vt1);
    vs0.name as vs1 -> vt0.name as vt1 then string(vs1, vt1);
    vs0.comment as vs1 -> vt0.comment as vt1 then string(vs1, vt1);
  };
  src.kind as vs -> tgt.kind as vt then string(vs, vt);
  src.abstract as vs -> tgt.abstract as vt then string(vs, vt);
  src.type as vs -> tgt.type as vt then string(vs, vt);
  src.baseDefinition as vs -> tgt.baseDefinition as vt then mapR4toSTU3Uri(vs, vt);
  src.contextInvariant as vs -> tgt.contextInvariant as vt then string(vs, vt);
  src.derivation as vs -> tgt.derivation as vt then string(vs, vt);
  
  // Transform R4 context objects to temporary extension for post-processing
  src.context as contextList -> tgt.extension as ext then createContextExtension(contextList, ext) "context-to-extension";
  
  src.snapshot as vs0 -> tgt.snapshot as vt0 then {
    vs0.element as vs1 -> vt0.element as vt1 then ElementDefinition(vs1, vt1);
  };
  src.differential as vs0 -> tgt.differential as vt0 then {
    vs0.element as vs1 -> vt0.element as vt1 then ElementDefinition(vs1, vt1);
  };
}

group mapR4toSTU3Uri(source src, target tgt) {

  // Default case: copy as-is for all other values - uses uri transformation
  src -> tgt then uri(src, tgt) "default-mapping";

  // R4 to STU3 URI mappings - specific mappings override default
  src where value = 'http://hl7.org/fhir/StructureDefinition/Observation' -> tgt.value = 'http://nictiz.nl/fhir/StructureDefinition/zib-GeneralMeasurement' "obs-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-AdvanceDirective' -> tgt.value = 'http://nictiz.nl/fhir/StructureDefinition/zib-AdvanceDirective' "advance-directive-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-ContactPerson' -> tgt.value = 'http://fhir.nl/fhir/StructureDefinition/nl-core-relatedperson' "contact-person-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-Encounter' -> tgt.value = 'http://nictiz.nl/fhir/StructureDefinition/zib-Encounter' "encounter-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-HealthProfessional-Practitioner' -> tgt.value = 'http://fhir.nl/fhir/StructureDefinition/nl-core-practitioner' "practitioner-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-HealthProfessional-PractitionerRole' -> tgt.value = 'http://fhir.nl/fhir/StructureDefinition/nl-core-practitionerrole' "practitioner-role-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-MedicalDevice' -> tgt.value = 'http://nictiz.nl/fhir/StructureDefinition/zib-MedicalDevice' "medical-device-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-MedicalDevice.Product' -> tgt.value = 'http://nictiz.nl/fhir/StructureDefinition/zib-MedicalDeviceProduct' "medical-device-product-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-Procedure-event' -> tgt.value = 'http://nictiz.nl/fhir/StructureDefinition/zib-Procedure' "procedure-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-TreatmentDirective2' -> tgt.value = 'http://nictiz.nl/fhir/StructureDefinition/zib-TreatmentDirective' "treatment-directive-mapping";
  src where value = 'http://nictiz.nl/fhir/StructureDefinition/nl-core-Patient' -> tgt.value = 'http://fhir.nl/fhir/StructureDefinition/nl-core-patient' "patient-mapping";

  // STU3 don't allow PractitionerRole as a reference for most elements because PractitionerRole was added too late in STU3. Therefore, we map it to Practitioner
  src where value = 'https://fhir.iknl.nl/fhir/StructureDefinition/ACP-HealthProfessional-PractitionerRole' -> tgt.value = 'https://fhir.iknl.nl/fhir/StructureDefinition/ACP-HealthProfessional-Practitioner' "practitionerrole-to-practitioner-reference-mapping";

}

group createContextExtension(source src, target ext) {
    src -> ext.url = 'http://fhir.conversion/cross-version/StructureDefinition.context' "context-extension-url";
    src.expression as expr -> ext.value = create('string') as extValue then string(expr, extValue) "context-expression-value";
}
